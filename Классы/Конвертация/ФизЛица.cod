class "";

import СИС2             Classes СтроковыеФункции,ФункцииДокумента;

inclass
--~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~--
  СтандартныеТипыАдреса[]       : Базовый.Справочники.ТипАдреса;
--~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~--
  proc ОпределитьСтандартныеТипыАдреса;
    var k       : integer;
    with Query.Create([Базовый.Справочники.ТипАдреса]) do
      Filter            = 'ДляФизлиц and (Код in ["Прописка","Фактический","Почтовый"])';
      Select;
      for k = 1..Count do
        СтандартныеТипыАдреса[k]                = Current as Базовый.Справочники.ТипАдреса;
        Next;
      end;
    end;
  end;--proc ОпределитьСтандартныеТипыАдреса
--~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~--
  --@doc Распознавание физиков выполняется по двум алгоитмам: с участием даты рождения и без.
  --@doc В случае, если ДР для поиска не указана, сравнения с нулевым значением не выполняется - идетификация только по ФИО.
  func ЗагруженоФл(locFm:string;locIm:string;locOt:string;locDr:date;var locRecord:Базовый.Данные.Субъект=nil):logical;
    var locFilter       : string;
    if   locDr<>nil then
      locFilter         = 'ФизическоеЛицо';
      locFilter         = locFilter+' and ФамилияФизлица="'+locFm+'"';
      locFilter         = locFilter+' and ИмяФизлица="'+locIm+'"';
      locFilter         = locFilter+' and ОтчествоФизлица="'+locOt+'"';
      locFilter         = locFilter+' and ДатаРождения='+Str(locDr);
      locRecord         = QueryRecord(Базовый.Данные.Субъект,locFilter) as Базовый.Данные.Субъект;
      Result            = locRecord<>nil;
    else
      locFilter         = 'ФизическоеЛицо';
      locFilter         = locFilter+' and ФамилияФизлица="'+locFm+'"';
      locFilter         = locFilter+' and ИмяФизлица="'+locIm+'"';
      locFilter         = locFilter+' and ОтчествоФизлица="'+locOt+'"';
      locRecord         = QueryRecord(Базовый.Данные.Субъект,locFilter) as Базовый.Данные.Субъект;
      Result            = locRecord<>nil;
    end;
  end;--func ЗагруженоФл
--~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~--
  --@doc При имопрте адресов в качестве ключа, по которому определяется совпадение адресов, используется
  --@doc структурированный адрес. В Т9 структурированный адрес как таковой в БД не хранится, есть только его элементы.
  --@doc Однако есть поле Адрес_Строкой, в котором предполагается хранение естественной формы записи адреса.
  --@doc При импорте это поле заполняется структурированным адресом и в дальнейшем используется как ключ поиска.
  --@doc
  --@doc             (Физлицо;СтрутурированныйАдрес(ключ поиска);ЗаписьАдреса)
  func ЗагруженАдрес(locSuject:Базовый.Данные.Субъект;locSetStrk:string;var locRecord:Базовый.Справочники.Адрес=nil):logical;
    var locFilter       : string;
      locFilter         = 'Субъект='+Str(locSuject);
      locFilter         = locFilter+' and Адрес_Строкой="'+locSetStrk+'"';
      locRecord         = QueryRecord(Базовый.Справочники.Адрес,locFilter) as Базовый.Справочники.Адрес;
      Result            = locRecord<>nil;
  end;
--~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~--
--~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~--
  --@doc При имопрте документов в качестве ключа, по которому определяется совпадение документа, используется
  --@doc строковая сумма реквизитов документа: КодТипаДокумента,СерияДокумент,НомерДоекмента.
  --@doc Ключ передается в функцию в виде массива строк.
  func ЗагруженДокументФл(locSuject:Базовый.Данные.Субъект;locKey:string[];var locRecord:Базовый.Справочники.Удостоверение=nil):logical;
    var locFilter       : string;
      locFilter         = 'ФизЛицо='+Str(locSuject);
      locFilter         = locFilter+' and ВидДокумента.Код="'+locKey[1]+'"';
      locFilter         = locFilter+' and Серия="'+locKey[2]+'"';
      locFilter         = locFilter+' and Номер="'+locKey[3]+'"';
      locRecord         = QueryRecord(Базовый.Справочники.Удостоверение,locFilter) as Базовый.Справочники.Удостоверение;
      Result            = locRecord<>nil;
  end;
--~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~--
  func ЗагруженоВсе:logical;
--    var locClassRecord  : Class Record;
--    Result      = true;
--    locClassRecord              = FindClass('ТБ69.ISF_ИСТФИН') as Class Record;
--    if locClassRecord<>nil then
--      with Query.Create([locClassRecord]) do
--        Select;
--        while not Eof do
--           if not Загружено(Current.КОД as string) then
--             Result       = false;
--             Break;
--           end;
--           Next;
--        end;--while not Eof
--      end;--with Query
--    end;
  end;--func ЗагруженоВсе

--~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~--
  func Выполнение:integer;
    var locClassRecord  : Class Record;
    var locRecordISF    : Record;
    var locFIO          : string;
    var locFm           : string;
    var locIm           : string;
    var locOt           : string;
    var locDr           : date;
    var RecordFL        : Базовый.Данные.Субъект;
    var locSNILS        : string;
    var locCountry      : Базовый.Справочники.Место;
    var locSetStrk      : string;
    var locDate         : date;
    var k               : integer;
    --Внутренние функции...........................................................................
    proc ПрисвоитьНенулевоеЗначение(locrecord:record;locFieldName:string;locValue:variant);
      if locValue<>nil then
        locrecord.SetField(locFieldName,locValue);
      end;
    end;--proc ПрисвоитьНенулевоеЗначение
    proc ЗаписатьСтандартныеТипыАдреса(RecordAdr: Базовый.Справочники.Адрес);
      var s                             : integer;
      var локСтандартныеТипыАдреса      : Базовый.Справочники.ТипАдреса[];
      for s = 1..RecordAdr.Позиции.Count do
        локСтандартныеТипыАдреса[s]     = RecordAdr.Позиции[s].ТипАдреса;
      end;
      for s = 1..LengthOfArray(СтандартныеТипыАдреса) do
        if not СтандартныеТипыАдреса[s] in локСтандартныеТипыАдреса then
          RecordAdr.Позиции.AddEx.ТипАдреса     = СтандартныеТипыАдреса[s];
        end;
      end;
      RecordPostIfNeed(RecordAdr);
    end;--proc ЗаписатьСтандартныеТипыАдреса
    proc ЗагрузкаАдреса;
      var RecordAdr     : Базовый.Справочники.Адрес;                      --
      var RecordElta    : Базовый.Классификаторы.Сокращение;              --
      if not ЗагруженАдрес(RecordFL,locSetStrk,RecordAdr)  then
        RecordAdr               = Базовый.Справочники.Адрес.Create;
        RecordAdr.Субъект       = RecordFL;
        locDate                 = locRecordISF.UpdateTime as Date;
        locDate                 = Dat(Day(locDate),Mon(locDate),Year(locDate));
        RecordAdr.Дата          = locDate;
        RecordAdr.Адрес_Строкой = locSetStrk;
        RecordAdr.Индекс        = locRecordISF.ИНДЕКСЖИТ as string;
        if locRecordISF.КОДСТРЖИТ='643' or locRecordISF.КОДСТРЖИТ='' then
          locCountry            = QueryRecord(Базовый.Справочники.Место,'КОД="643"') as Базовый.Справочники.Место;
        else
          locCountry            = QueryRecord(Базовый.Справочники.Место,'КОД="'+locRecordISF.КОДСТРЖИТ as string+'"') as Базовый.Справочники.Место;
        end;
        RecordAdr.Страна                = locCountry;
        RecordAdr.Регион_Стр            = locRecordISF.РЕГИОН as string;
        RecordAdr.Регион_Код            = locRecordISF.КОДРЕГЖИТ as string;
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.КОДРЕГЖИТ as string        +'" and Уровень=1') as Базовый.Классификаторы.Сокращение;
        RecordAdr.Регион_Элемент        = RecordElta;
        RecordAdr.Район                 = locRecordISF.РАЙОН as string;
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.РАЙОН_СОКР as string       +'" and Уровень=2') as Базовый.Классификаторы.Сокращение;
        RecordAdr.Район_Элемент         = RecordElta;
        RecordAdr.Город                 = locRecordISF.ГОРОД as string;
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.ГОРОД_СОКР as string       +'" and Уровень=3') as Базовый.Классификаторы.Сокращение;
        RecordAdr.Город_Элемент         = RecordElta;
        RecordAdr.НасПункт              = locRecordISF.НАСПУНКТ as string;
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.НАСПУНКТ_СОКР as string    +'" and Уровень=4') as Базовый.Классификаторы.Сокращение;
        RecordAdr.Пункт_Элемент         = RecordElta;
        RecordAdr.Улица                 = locRecordISF.УЛИЦА as string;
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.УЛИЦА_СОКР as string       +'" and Уровень=5') as Базовый.Классификаторы.Сокращение;
        RecordAdr.Улица_Элемент         = RecordElta;
        RecordAdr.Дом                   = locRecordISF.ДОМ as string;
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.ДОМ_СОКР as string         +'" and Уровень=6') as Базовый.Классификаторы.Сокращение;
        RecordAdr.Дом_Элемент           = RecordElta;
        RecordAdr.Корпус                = locRecordISF.КОРП as string;
        RecordAdr.Квартира              = locRecordISF.КВАРТ as string;
        RecordAdr.Телефон               = locRecordISF.ТЕЛЕФОН as string;
        RecordAdr.Post;
        ЗаписатьСтандартныеТипыАдреса(RecordAdr);
      else--обновление записи
        locDate                         = locRecordISF.UpdateTime as Date;
        locDate                         = Dat(Day(locDate),Mon(locDate),Year(locDate));
        if locDate>RecordAdr.Дата then
          RecordAdr.Дата                = locDate;
        end;
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Индекс'           ,locRecordISF.ИНДЕКСЖИТ as string);
        locCountry                      = QueryRecord(Базовый.Справочники.Место,'КОД="'+locRecordISF.КОДСТРЖИТ as string+'"') as Базовый.Справочники.Место;
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Страна'           ,locCountry);
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Регион_Стр'       ,locRecordISF.РЕГИОН);
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Регион_Код'       ,locRecordISF.КОДРЕГЖИТ);
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.КОДРЕГЖИТ as string        +'" and Уровень=1') as Базовый.Классификаторы.Сокращение;
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Регион_Элемент'   ,RecordElta);
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Район'            ,locRecordISF.РАЙОН);
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.РАЙОН_СОКР as string       +'" and Уровень=2') as Базовый.Классификаторы.Сокращение;
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Район_Элемент',RecordElta);
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Город'            ,locRecordISF.ГОРОД);
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.ГОРОД_СОКР as string       +'" and Уровень=3') as Базовый.Классификаторы.Сокращение;
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Город_Элемент'    ,RecordElta);
        ПрисвоитьНенулевоеЗначение(RecordAdr,'НасПункт'         ,locRecordISF.НАСПУНКТ);
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.НАСПУНКТ_СОКР as string    +'" and Уровень=4') as Базовый.Классификаторы.Сокращение;
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Пункт_Элемент'    ,RecordElta);
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Улица'            ,locRecordISF.УЛИЦА);
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.УЛИЦА_СОКР as string       +'" and Уровень=5') as Базовый.Классификаторы.Сокращение;
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Улица_Элемент'    ,RecordElta);
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Дом'              ,locRecordISF.ДОМ);
        RecordElta                      = QueryRecord(Базовый.Классификаторы.Сокращение,'КОД="'+locRecordISF.ДОМ_СОКР as string         +'" and Уровень=6') as Базовый.Классификаторы.Сокращение;
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Дом_Элемент'      ,RecordElta);
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Корпус'           ,locRecordISF.КОРП);
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Квартира'         ,locRecordISF.КВАРТ);
        ПрисвоитьНенулевоеЗначение(RecordAdr,'Телефон'          ,locRecordISF.ТЕЛЕФОН);
        ЗаписатьСтандартныеТипыАдреса(RecordAdr);
        RecordPostIfNeed(RecordAdr);
      end;
    end;--proc ЗагрузкаАдреса
    proc ЗагрузкаДокументовФл(ВнешняяЗаписьФл:integer);           --Документ идентифицируется по строковой сумме полей Код,Серия,Номер.
      var locKey        : string[];
      var RecordDoc     : Базовый.Справочники.Удостоверение;
      var RecordDocType : Базовый.Классификаторы.ВидДокумента;
      var k             : integer;
      with Query.Create([FindClass('ТБ69.ISF_СОТР_ДОК') as Class Record]) do
        Filter          = 'ФЛ='+Str(ВнешняяЗаписьФл);
        Select;
        for k = 1..Count do
          locKey[1]     = Current.GetField('КОДТИПДОК') as string;
          locKey[2]     = Trim(Current.GetField('СЕР') as string);
          locKey[3]     = Trim(Current.GetField('НОМ') as string);
          if not ЗагруженДокументФл(RecordFL,locKey,RecordDoc)  then
            RecordDoc                   = Базовый.Справочники.Удостоверение.Create;
            RecordDoc.ФизЛицо           = RecordFL;
            RecordDoc.ОсновнойДок       = true;
            RecordDocType               = QueryRecord(Базовый.Классификаторы.ВидДокумента,'КОД="'+locKey[1]+'"') as Базовый.Классификаторы.ВидДокумента;
            RecordDoc.ВидДокумента      = RecordDocType;
            RecordDoc.Серия             = Current.GetField('Сер')       as string;
            RecordDoc.Номер             = Current.GetField('Ном')       as string;
            RecordDoc.Кем               = Current.GetField('Кем')       as string;
            RecordDoc.ДатаВыдачи        = Current.GetField('ДАТВЫД')    as date;
            RecordDoc.Post;
          else
            ПрисвоитьНенулевоеЗначение(RecordDoc,'Кем'                  ,Current.GetField('Кем') as string);
            ПрисвоитьНенулевоеЗначение(RecordDoc,'ДатаВыдачи'           ,Current.GetField('ДАТВЫД') as date);
            RecordPostIfNeed(RecordDoc);
          end;
          Next;
        end;
      end;--with Query
    end;--proc ЗагрузкаДокументовФл
    --Окончание внутренних функций.................................................................
    ОпределитьСтандартныеТипыАдреса;
    locClassRecord              = FindClass('ТБ69.ISF_ФЛ') as Class Record;
    if locClassRecord=nil then
      Message('Класс ISF_ФЛ не найден.');
    else
      with Query.Create([locClassRecord]) do
        Select;
        while not Eof do
          locRecordISF  = Current as record;
          locFIO        = Current.ФИОТ as string;
          locFm         = СБольшойБуквы(Trim(ExtractWord(locFIO,1)));
          locIm         = СБольшойБуквы(Trim(ExtractWord(locFIO,2)));
          locOt         = СБольшойБуквы(Trim(ExtractWord(locFIO,3)));
          locFIO        = locFm+' '+locIm+' '+locOt;
          locDr         = Current.ДРОЖД as Date;
          if Current.НПФ1<>nil and Current.НПФ2<>nil and Current.НПФ3<>nil and Current.НПФ4<>nil then
            locSNILS    = (Current.НПФ1+'-'+Current.НПФ2+'-'+Current.НПФ3+' '+Current.НПФ4) as string;
          else
            locSNILS    = nil;
          end;
          if not ЗагруженоФл(locFm,locIm,locOt,locDr,RecordFL)  then
            --создание и заполнение основной записи
            RecordFL                    = Базовый.Данные.Субъект.Create;
            RecordFL.ФизическоеЛицо     = true;
            RecordFL.Код                = ТББ_Базовый.Нумератор.НовыйКод(RecordFL);
            RecordFL.Имя                = locFIO;
            RecordFL.Наим               = locFIO;
            RecordFL.ФамилияФизлица     = locFm;
            RecordFL.ИмяФизлица         = locIm;
            RecordFL.ОтчествоФизлица    = locOt;
            RecordFL.ДатаРождения       = locDr;
            RecordFL.Пол                = if(Current.Пол='М',1,2);
            RecordFL.ИНН                = Current.ИННФ as string;
            RecordFL.КодГНИ             = Current.КОДГНИФ as string;
            RecordFL.НалоговыйСтатусФл  = 1;
            RecordFL.НомерПфр           = locSNILS;
            if Current.ГРАЖДАНСТВО='РОССИЯ' or Current.ГРАЖДАНСТВО='' then
              locCountry                = QueryRecord(Базовый.Справочники.Место,'КОД="643"') as Базовый.Справочники.Место;
              RecordFL.НалоговыйСтатусФл= 1;
            else
              locCountry                = QueryRecord(Базовый.Справочники.Место,'Имя="'+Current.ГРАЖДАНСТВО as string+'"') as Базовый.Справочники.Место;
              RecordFL.НалоговыйСтатусФл= 2;
            end;
            RecordFL.ГражданствоФл      = locCountry;
            RecordFL.Post;
            --загрузка адресной информации
            locSetStrk                  = Current.АДРЕС_СТРУКТ as string;
            ЗагрузкаАдреса;


          else --запись есть
            --обновление основной записи
            ПрисвоитьНенулевоеЗначение(RecordFL,'ФамилияФизлица'        ,locFm);
            ПрисвоитьНенулевоеЗначение(RecordFL,'ИмяФизлица'            ,locIm);
            ПрисвоитьНенулевоеЗначение(RecordFL,'ОтчествоФизлица'       ,locOt);
            ПрисвоитьНенулевоеЗначение(RecordFL,'ДатаРождения'          ,locDr);
            ПрисвоитьНенулевоеЗначение(RecordFL,'Имя'                   ,locFIO);
            ПрисвоитьНенулевоеЗначение(RecordFL,'Наим'                  ,locFIO);
            RecordFL.Пол                = if(Current.Пол='М',1,2);
            ПрисвоитьНенулевоеЗначение(RecordFL,'ИНН'                   ,Current.ИННФ as string);
            ПрисвоитьНенулевоеЗначение(RecordFL,'КодГНИ'                ,Current.КОДГНИФ as string);
            ПрисвоитьНенулевоеЗначение(RecordFL,'НомерПфр'              ,locSNILS);
--            if Current.ГРАЖДАНСТВО='РОССИЯ' or Current.ГРАЖДАНСТВО='' then
--              locCountry                = QueryRecord(Базовый.Справочники.Место,'КОД="643"');
--              RecordFL.НалоговыйСтатусФл= 1;
--            else
--              locCountry                = QueryRecord(Базовый.Справочники.Место,'Имя="'+Current.ГРАЖДАНСТВО+'"');
--              RecordFL.НалоговыйСтатусФл= 2;
--            end;
--            RecordFL.ГражданствоФл      = locCountry;
            RecordPostIfNeed(RecordFL);
            --обновление адресной информации
            locSetStrk                  = Current.АДРЕС_СТРУКТ as string;
            ЗагрузкаАдреса;
            ЗагрузкаДокументовФл(Current.DocId as integer);


          end; --тест на выполнение загрузки
          k     = k+1;
          Hint('Выполнение...',k,Count);
          Next;
        end;
      end;--with Query
    end;
--    if ЗагруженоВсе then
--      Result            = cmOk;
--    end;
  end;--func Выполнение



inobject


end